/*
 ESP8266 MQTT Single Relay controller
 Author: Vikash Chauhan
 
 Description: Control common electric roller shutter doors (or similar) 
 which are normally toggled open/close via a remote key fob.  Using
 an esp8266 module and a single relay module we can hook this up to the
 garage door control box (check to see if additional triggers can be added)
 and send it an 'action' to momentarily set the relay, thus 
 toggling the door mechanism.
*/

#include <ESP8266WiFi.h>
#include <PubSubClient.h>

const char* ssid = "xxx";
const char* password = "xxx";
const char* mqtt_server = "1.2.3.4";

const char* in_topic = "garage/door/1";
const char* in_action_toggle = "toggleOpenClose";
const int relay_signal_pin = 5;

WiFiClient espClient;
PubSubClient client(espClient);

void setup_wifi() {
  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* bytePayload, unsigned int length) {
  String payload;
  
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  for (int i = 0; i < length; i++) {
    payload += (char)bytePayload[i];
  }
  Serial.print(payload);
  Serial.println();

  if (payload == in_action_toggle) {
    // switch led and output pin on
    digitalWrite(BUILTIN_LED, LOW);
    digitalWrite(relay_signal_pin, HIGH);
    
    // switch off after small delay (momentary action)
    delay(250);
    digitalWrite(BUILTIN_LED, HIGH);
    digitalWrite(relay_signal_pin, LOW);
  } else {
    Serial.print("Unknown action: ");
    Serial.print(payload);
    Serial.println();
  }
}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Create a random client ID
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    // Attempt to connect
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      client.subscribe(in_topic);
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  pinMode(BUILTIN_LED, OUTPUT);
  pinMode(relay_signal_pin, OUTPUT);
  
  Serial.begin(115200);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();
}
